package MABISZCalculateRules

import hu.dbx.ws.model.facts.*;
import hu.dbx.ws.model.*;
import org.joda.time.DateTime;
import java.util.Date;

function boolean isNullString(String s) {
		return ("".equals(s) || s == null);
}

function int getYear(Date d) {
	if (d != null) {
		DateTime date = new DateTime(d);
		return date.getYear();
	}
	
	return 0;	
}

function boolean isNullInt(Integer i) {
		return (i == null || i == 0);
}

function boolean firstDateIsAfter(Date s0, Date e0) {
	if (s0 == null || e0 == null) {
		return false;
	}
	
	DateTime s = new DateTime(s0);
	DateTime e = new DateTime(e0);	
	
	int millisOfDay = s.getMillisOfDay();
	s = s.minusMillis(millisOfDay);
	millisOfDay = e.getMillisOfDay();
	e = e.minusMillis(millisOfDay);
	
	return s.isAfter(e);    
}

function boolean isBetween(Integer lowerBound, Integer upperBound, Integer value) {
	if ( value == null ) {
		return false;
	} 
	else {
		return (lowerBound <= value && upperBound >= value);
	}
}

function String getFormattedDate(Date d) {
		DateTime d1 = new DateTime(d);
		String date = String.valueOf(d1.getYear());
		if (d1.getMonthOfYear() < 10) {
			date = date + "-0" + String.valueOf(d1.getMonthOfYear() );
		}
		else {
			date = date + "-" + String.valueOf(d1.getMonthOfYear() );
		}
		if (d1.getDayOfMonth() < 10) {
			date = date + "-0" + String.valueOf(d1.getDayOfMonth() );
		}
		else {
			date = date + "-" + String.valueOf(d1.getDayOfMonth() );
		}
				
		return date;
}

//----------------------------------------
//----------------------------------------
rule "validate startDate (isNull)"
    agenda-group "validate"
      
	when
		$q : Query( startDate == null )
				
	then
		insert( Message.create("1") );		
end

rule "validate startDate (< 2010.01.01)"
    agenda-group "validate"
    salience 20  
	when
		$q : Query( startDate != null && eval (getYear( startDate ) < 2010 ) )
				
	then
		insert( Message.create("3", "2010") );		
end

rule "validate endDate (isNull)"
    agenda-group "validate"
      
	when
		$q : Query( endDate == null )
				
	then
		insert( Message.create("2") );		
end

rule "validate endDate (> 2011.12.31)"
    agenda-group "validate"
    salience 20  
	when
		$q : Query( endDate != null && eval (getYear( endDate ) > 2011 ) )
				
	then
		insert( Message.create("18", "2011") );		
end

rule "validate startDate > endDate"
    agenda-group "validate"
    salience 20 
	when
		$q : Query( startDate != null && endDate != null )
		eval (firstDateIsAfter( $q.getStartDate(), $q.getEndDate() ) )
				
	then
		insert( Message.create("17", getFormattedDate( $q.getStartDate() ), getFormattedDate( $q.getEndDate() ) ) );		
end

/*****************************************************************************
				        Jármű szintű validációk 
*****************************************************************************/
rule "validate vehicle typecode (isNull)"
    agenda-group "validate"
      
	when
		$v : Vehicle( eval( isNullString(typeCode) ) )
		
	then
		$v.setTypeCode(null);
		insert( Message.create("4") );		
end

rule "validate vehicle typecode (not valid)"
    agenda-group "validate"
      
	when
		$v : Vehicle( eval( !isNullString(typeCode) ) )
		not(exists VehicleTypeDef( typeCode == $v.typeCode ) )
		
	then
		insert( Message.create("5", $v.getTypeCode() ) );		
end

rule "validate power (isNull)"
    agenda-group "validate"
      
	when
		$c1: Constant( name == "TC_SZEMELYGEPJARMU")
		$c2: Constant( name == "TC_MOTORKEREKPAR")
		$q : Query()
		$v : Vehicle( (typeCode == $c1.value || typeCode == $c2.value) && 
					  eval( isNullInt( maximumNettoPower ) ) )
		eval ( getYear( $q.getEndDate() ) == 2011 )
	then
		$v.setMaximumNettoPower(null);
		insert( Message.create("6") );
end

rule "validate power (not valid)"
    agenda-group "validate"
      
	when
		$c1: Constant( name == "TC_SZEMELYGEPJARMU")
		$c2: Constant( name == "TC_MOTORKEREKPAR")
		$q : Query()
		$v : Vehicle( (typeCode == $c1.value || typeCode == $c2.value) && 
					  eval( !isNullInt( maximumNettoPower ) ) )
		eval( $v.getMaximumNettoPower() < 0 || $v.getMaximumNettoPower() > 999999)
		eval ( getYear( $q.getEndDate() ) == 2011 )
	then
		$v.setMaximumNettoPower(null);
		insert( Message.create("7") );
end

rule "validate cubicCapacity (isNull)"
    agenda-group "validate"
      
	when
		$c1: Constant( name == "TC_SZEMELYGEPJARMU")
		$c2: Constant( name == "TC_MOTORKEREKPAR")
		$q : Query()
		$v : Vehicle( (typeCode == $c1.value || typeCode == $c2.value) && 
					  eval( isNullInt(cubicCapacity) && !cubicCapacityNotDefined) )				  
		eval ( getYear( $q.getStartDate() ) == 2010 )
			
	then
		$v.setCubicCapacity(null);
		insert( Message.create("8") );
end

rule "validate cubicCapacity (not valid)"
    agenda-group "validate"
      
	when
		$c1: Constant( name == "TC_SZEMELYGEPJARMU")
		$c2: Constant( name == "TC_MOTORKEREKPAR")
		$q : Query()
		$v : Vehicle( (typeCode == $c1.value || typeCode == $c2.value) &&  
					  eval( !isNullInt(cubicCapacity) ) )
		eval( $v.getCubicCapacity() < 0 || $v.getCubicCapacity() > 999999)
		eval ( getYear( $q.getStartDate() ) == 2010 )
		
	then
		$v.setCubicCapacity(null);
		insert( Message.create("9") );
end

rule "validate seatingCapacity (isNull)"
    agenda-group "validate"
      
	when
		$c1: Constant( name == "TC_AUTOBUSZ")
		$v : Vehicle( typeCode == $c1.value && 
					  eval( isNullInt(seatingCapacity) ) )
			
	then
		$v.setSeatingCapacity(null);
		insert( Message.create("10") );
end

rule "validate seatingCapacity (not valid)"
    agenda-group "validate"
      
	when
		$c1: Constant( name == "TC_AUTOBUSZ")
		$v : Vehicle( typeCode == $c1.value && eval( !isNullInt(seatingCapacity) ) )
		eval( $v.getSeatingCapacity() > 999999 || $v.getSeatingCapacity() < 10 )

	then
		$v.setSeatingCapacity(null);
		insert( Message.create("11") );
end

rule "validate carryingCapacity (isNull)"
    agenda-group "validate"
      
	when
		$c1: Constant( name == "TC_TEHERGEPJARMU")
		$q : Query()
		$v : Vehicle( typeCode == $c1.value && 
					  eval( isNullInt(carryingCapacity) ) )
		eval ( getYear( $q.getStartDate() ) == 2010 )
			
	then
		$v.setCarryingCapacity(null);
		insert( Message.create("12") );
end

rule "validate carryingCapacity (not valid)"
    agenda-group "validate"
      
	when
		$c1: Constant( name == "TC_TEHERGEPJARMU")
		$q : Query()
		$v : Vehicle( typeCode == $c1.value && eval( !isNullInt(carryingCapacity) ) )
		eval( $v.getCarryingCapacity() > 999999 || $v.getCarryingCapacity() < 0 )
		eval ( getYear( $q.getStartDate() ) == 2010 )

	then
		$v.setCarryingCapacity(null);
		insert( Message.create("13") );
end

rule "validate maximumAllowedWeight (isNull)"
    agenda-group "validate"
      
	when
		$c1: Constant( name == "TC_TEHERGEPJARMU")
		$q : Query()
		$v : Vehicle( typeCode == $c1.value && 
					  eval( isNullInt(maximumAllowedWeight) ) )
		eval ( getYear( $q.getEndDate() ) == 2011 )
	then
		$v.setMaximumAllowedWeight(null);
		insert( Message.create("14") );

end

rule "validate maximumAllowedWeight (not valid)"
    agenda-group "validate"
      
		when
		$c1: Constant( name == "TC_TEHERGEPJARMU")
		$q : Query()
		$v : Vehicle( typeCode == $c1.value && 
					  eval( !isNullInt(maximumAllowedWeight) ) )
		eval( $v.getMaximumAllowedWeight() > 999999 || $v.getMaximumAllowedWeight() < 0 )					  
		eval ( getYear( $q.getEndDate() ) == 2011 )	
	then
		$v.setMaximumAllowedWeight(null);
		insert( Message.create("15") );

end
